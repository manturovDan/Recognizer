%package man.dan.smc

%class RecognizerCodegen

%access public
%start MAP_NAME::StartState

%map MAP_NAME %%
    StartState {

        smallLetter(letter : char)
        GetHead {
            addToHead(letter);
        }

        Default
        Error {}
    }

    GetHead {
        smallLetter(letter : char) [isPlaceInHeader()]
        GetHead {
            addToHead(letter);
        }

        colon [correctHeader()]
        InputNum {
            newNum();
        }

        Default
        Error {}
    }

    InputNum {
        digit (dig : int) [AddNumCorrectness()]
        InputNum
        {
            addDigitToNum(dig);
        }

        comma [endOfNum()]
        InputNum {
            saveNum();
            newNum();
        }

        semicolon [endOfNum()]
        bodyAvailable {
            saveNum();
        }

        Default
        Error {}
    }

    bodyAvailable {
        EOS
        Correct { rowCorrect(); }

        question [isBodyEn()]
        BodyQ { }

        Default
        Error {}
    }

    BodyQ {
        smallLetter(letter : char) [letter == 'b']
        BodyB {}

        Default
        Error {}
    }

    BodyB {
        smallLetter(letter : char) [letter == 'o']
        BodyO {}

        Default
        Error {}
    }

    BodyO {
        smallLetter(letter : char) [letter == 'd']
        BodyD {}

        Default
        Error {}
    }

    BodyD {
        smallLetter(letter : char) [letter == 'y']
        BodyY {}

        Default
        Error {}
    }

    BodyY {
        equal
        inBody {}

        Default
        Error {}
    }

    inBody {
        //we don't save the body and may to do it if we want
        smallLetter [bodyInputEnable()]
        inBody { newBodySymbol(); }

        digit [bodyInputEnable()]
        inBody { newBodySymbol(); }

        bigLetter [bodyInputEnable()]
        inBody { newBodySymbol(); }

        comma [bodyInputEnable()]
        inBody { newBodySymbol(); }

        percentOrExclamationoOrDot [bodyInputEnable()]
        inBody { newBodySymbol(); }

        question [bodyInputEnable()]
        inBody { newBodySymbol(); }

        EOS[notEmptyBody()]
        Correct { rowCorrect(); }

        Default
        Error {}
    }

    Correct {

    }

    Error {
        error
        nil {}
    }

    NextState {}
%%