%package man.dan.smc

%class RecognizerCodegenSMContext
%fsmclass RecognizerCodegenStateMachine

%access public
%start MAP_NAME::StartState

%map MAP_NAME %%
    StartState {

        smallLetter(letter : char)
        GetHead {
            addToHead(letter);
        }

        Default
        Error {}
    }

    GetHead {
        smallLetter(letter : char) [isPlaceInHeader()]
        GetHead {
            addToHead(letter);
        }

        colon [correctHeader()]
        InputNum {
            newNum();
        }

        Default
        Error {}
    }

    InputNum {
        digit (dig : int) [AddNumCorrect()]
        InputNum
        {
            addDigitToNum(dig);
        }

        comma [endOfNum()]
        InputNum {
            saveNum();
            newNum();
        }

        semicolon [endOfNum()]
        bodyAvailable {
            saveNum();
        }

        Default
        Error {}
    }

    bodyAvailable {
        EOS
        Correct {}

        question [isBodyEn()]
        BodyQ { }

        Default
        Error {}
    }

    BodyQ {
        smallLetter(letter : char) [isLetter("b")]
        BodyB {}

        Default
        Error {}
    }

    BodyB {
        smallLetter(letter : char) [isLetter("o")]
        BodyO {}

        Default
        Error {}
    }

    BodyO {
        smallLetter(letter : char) [isLetter("d")]
        BodyD {}

        Default
        Error {}
    }

    BodyD {
        smallLetter(letter : char) [isLetter("y")]
        BodyY {}

        Default
        Error {}
    }

    BodyY {
        equal
        inBody {}

        Default
        Error {}
    }

    inBody {
        //we don't save the body and may to do it if we want
        smallLetter [bodyInputEnable()]
        inBody { newBodySymbol(); }

        digit [bodyInputEnable()]
        inBody { newBodySymbol(); }

        bigLetter [bodyInputEnable()]
        inBody { newBodySymbol(); }

        comma [bodyInputEnable()]
        inBody { newBodySymbol(); }

        percentOrExclamationoOrDot [bodyInputEnable()]
        inBody { newBodySymbol(); }

        question [bodyInputEnable()]
        inBody { newBodySymbol(); }

        EOS[notEmptyBody()]
        Correct {}

        Default
        Error {}
    }

    Correct {

    }

    Error {

    }

    NextState {}
%%